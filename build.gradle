plugins {
    id 'java'
    id 'jacoco'
    id 'com.github.spotbugs' version '2.0.0'
    id 'pmd'
    id 'checkstyle'
    id 'maven-publish'
    id 'signing'
}

group 'com.levelrin'
version '1.0.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.0'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}

test {
    useJUnitPlatform()
    dependsOn 'cleanTest'
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat = 'full'
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                enabled = true
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.90000
            }
            limit {
                enabled = true
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.90000
            }
            limit {
                enabled = true
                counter = 'COMPLEXITY'
                value = 'COVEREDRATIO'
                minimum = 0.90000
            }
            limit {
                enabled = true
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.90000
            }
            limit {
                enabled = true
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.90000
            }
            limit {
                enabled = true
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 0.90000
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

check.dependsOn jacocoTestCoverageVerification
check.dependsOn jacocoTestReport

spotbugs {
    toolVersion = '3.1.12'
    ignoreFailures = false
    effort = 'max'
}
tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

pmd {
    toolVersion = '6.16.0'
    ignoreFailures = false
    ruleSetFiles = files('config/pmd/ruleSet.xml')
    ruleSets = []
}
tasks.withType(Pmd) {
    reports {
        xml.enabled true
        html.enabled true
    }
}

checkstyle {
    toolVersion '8.22'
    ignoreFailures = false
    configFile = file('config/checkstyle/checkstyle.xml')
    configProperties 'checkstyle.header.file': file('config/checkstyle/java.header')
}
tasks.withType(Checkstyle) {
    reports {
        xml.enabled true
        html.enabled true
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'javasinglevalue'
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'JavaSingleValue'
                description = 'Java objects that represent singe value.'
                url = 'https://github.com/levelrin/JavaSingleValue'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/levelrin/JavaSingleValue/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'levelrin'
                        name = 'Rin'
                        email = 'levelrin@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:levelrin/JavaSingleValue.git'
                    developerConnection = 'scm:git:git@github.com:levelrin/JavaSingleValue.git'
                    url = 'https://github.com/levelrin/JavaSingleValue'
                }
            }

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }

    repositories {
        maven {
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            credentials {
                def ossrhUser = project.hasProperty('ossrhUser') ? ossrhUser : 'fakeUser'
                def ossrhPassword = project.hasProperty('ossrhPassword') ? ossrhPassword : 'fakePassword'

                username ossrhUser
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
